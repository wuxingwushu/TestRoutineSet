#version 450

//layout(local_size_x_id = 0) in;             // workgroup size set with specialization constant
layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

//#define Color_ -460365120
#define PI 1.570796326795
layout(std430, binding = 0) buffer buf//画布
{
   int arr_xy[];
};

layout(std430, binding = 1) buffer ObjectUniform //描述符
{
   uint size;//射线的数量
   int x;//玩家位置
   int y;
   uint y_size;//图像大小的Y轴像素数量    
   float angel;//角度
   int Color;//颜色
} params;

layout(std430, binding = 2) buffer wall//墙壁
{
   int wall_xy[];
};

int color = 0;//射线默认颜色

bool dianshe(uint xx, uint yy){
     uint lxx = xx * params.y_size + yy;//当前点
     arr_xy[lxx] = color;
     return wall_xy[lxx] == 1 ? true : false;
     //return wall_xy[lxx];
}

void bresenham_line(int x0, int y0, int x1, int y1) {
    int dx = abs(x1 - x0);
    int dy = abs(y1 - y0);
    int sx = (x0 < x1) ? 1 : -1;
    int sy = (y0 < y1) ? 1 : -1;
    int err = dx - dy;
    int e2;
    while (true) {
        if(dianshe(x0,y0)) break;
        if (x0 == x1 && y0 == y1) break; // 线条绘制完毕
        e2 = 2 * err;
        if (e2 > -dy) {
            err -= dy;
            x0 += sx;
        }
        if (x0 == x1 && y0 == y1) {
            dianshe(x0,y0);
            break;
        }
        if (e2 < dx) {
            err += dx;
            y0 += sy;
        }
    }
}

void main(){
    //const uint id_x = 0;
    const uint id_x = gl_GlobalInvocationID.x; // current offset
    //const uint id_y = gl_GlobalInvocationID.z;
    if(params.size <= (id_x)){                   // drop threads outside the buffer
       return;
    }
    uint shexian = params.size/2;
    if((id_x <= shexian+3) && (id_x >= shexian-3)){//中间几根射线更换颜色
        color = params.Color;
    }
    float du = params.angel + (float(PI / params.size) * id_x);//获取角度
    int x = int(cos(du) * 400);
    int y = int(sin(du) * 400);
    bresenham_line(params.x,params.y,params.x+x,params.y+y);//射线检测
    return;
}
